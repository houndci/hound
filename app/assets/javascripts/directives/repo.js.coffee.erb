App.directive 'repo', ['Subscription', 'StripeCheckout', (Subscription, StripeCheckout) ->
  scope: true
  templateUrl: '/templates/repo'
  link: (scope, element, attributes) ->
    enableRepo = ->
      scope.processing = true

      scope.repo.$enable()
        .catch(-> alert('Your repo failed to enable.'))
        .finally(-> scope.processing = false)

    disableRepo = ->
      scope.processing = true

      scope.repo.$disable()
        .catch(-> alert('Your repo failed to disable.'))
        .finally(-> scope.processing = false)

    createSubscription = (stripeToken) ->
      scope.processing = true

      subscription = new Subscription(
        repo_id: scope.repo.id
        card_token: stripeToken.id
        email_address: stripeToken.email
      )

      subscription.$save().then((response) ->
        scope.repo.enabled = true
        scope.repo.stripe_subscription_id = response.stripe_subscription_id
      ).catch(->
        alert('Your subscription failed.')
      ).finally(->
        scope.processing = false
      )

    deleteSubscription = ->
      scope.processing = true

      subscription = new Subscription(repo_id: scope.repo.id)

      subscription.$delete().then(->
        scope.repo.enabled = false
        scope.repo.stripe_subscription_id = null
      ).catch(->
        alert('Your repo could not be disabled')
      ).finally(->
        scope.processing = false
      )

    scope.toggle = ->
      if scope.repo.enabled
        if scope.repo.stripe_subscription_id
          deleteSubscription()
        else
          disableRepo()
      else
        if scope.repo.price_in_cents > 0
          StripeCheckout.open(
            name: scope.repo.full_plan_name,
            amount: scope.repo.price_in_cents,
            createSubscription
          )
        else
          enableRepo()
]
